model{

#Observation Level
for (i in 1:Nobs){
	#Uncertainties
	ln_Conc[i]~dnorm(ln_concmu[i], concPrec[i])
	
	#Interrelationships
	ln_concmu[i]<-ln_Conc0[Exp[i]]+log(alpha[Exp[i]]*exp(-k1[Exp[i]]*Time[i])+(1-alpha[Exp[i]])*exp(-k2[Exp[i]]*Time[i]))+ErrC[i]
	
	ErrC[i]~dnorm(0,1.E-1)
	}
#Experiment Level
for (j in 1:Nexps){
	#Uncertainties
	#Make mu.ln_Conc0 and tau.ln_Conc0 fixed	
	ln_Conc0[j]~dnorm(mu.ln_Conc0[j], tau.ln_Conc0[j])
		
	#Interrelationships
	k1[j]<-k1_20*exp(lambda1*(Temp[j]-20))+Errk1[Stud[j]]
	k2[j]<-k2_20*exp(lambda2*(Temp[j]-20))+Errk2[Stud[j]]
	}
for (j in 1:Nexpsbiph){
	alpha[j]~dbeta(alpha.alpha[Stud[j]], beta.alpha[Stud[j]])
	}
for (j in Monoexpstart:Nexps){
	alpha[j]~dbern(theta.alpha[Stud[j]])
	}

#Study level priors
for (k in 1:Nstud){
	Errk1[k]~dnorm(0,1.E-1)
	Errk2[k]~dnorm(0,1.E-1)
	}
for (k in 1:NStudBiph){
	alpha.alpha[k]~dunif(1,20)
	beta.alpha[k]~dunif(1,20)
	}
for (k in Monostudstart:Nstud){
	theta.alpha[k]~dbeta(1,1)
	}

#Universal priors	
k1_20~dnorm(0,1.E-6)
k2_20<-k1_20-delt
delt<-exp(delta)
delta~dnorm(0,1.E-6)
lambda1~dnorm(0,1.E-6)
lambda2~dnorm(0,1.E-6)
}

