#This work is licensed under a Creative Commons Attribution-NonCommercial 4.0 International License 
#<http://creativecommons.org/licenses/by-nc/4.0/> by Philip Collender, Christopher Hoover, Howard Chang,
#and Justin Remais. This work was supported in part by the National Institutes of Health/National Science 
#Foundation Ecology of Infectious Disease program funded by the Fogarty International Center (grant R01TW010286), 
#the National Institute of Allergy and Infectious Diseases (grant K01AI091864), and the National Science Foundation 
#Water Sustainability and Climate Program (grant 1360330).

#Per the terms of this license, if you are making derivative use of this work, you must identify that your work is a derivative work, 
#give credit to the original work, provide a link to the license, and indicate changes that were made.

model{

#Observation Level
for (i in 1:Nobs){
	#Uncertainties
	ln_Conc[i]~dnorm(mu.ln_Conc[i], tau.ln_Conc[i])
	
	#Interrelationships
	mu.ln_Conc[i]<-ln_Conc0[Exp[i]]+log(alpha[Exp[i]]*exp(-K1[Exp[i]]*Time[i])+(1-alpha[Exp[i]])*exp(-K2[Exp[i]]*Time[i]))+ErrC[i]
	
	ErrC[i]~dnorm(0,1.E-2)
	}
#Experiment Level
for (j in 1:NExps){
	#Uncertainties
	#Make mu.ln_Conc0 and tau.ln_Conc0 fixed	
	ln_Conc0[j]~dnorm(mu.ln_Conc0[j], tau.ln_Conc0[j])
		
	#Interrelationships
	K1[j]~dnorm(mu.K1[j],tau.K1[j])
        mu.K1[j]<-K1_20*exp(lambda1*(Temp[j]-20))

	K2[j]~dnorm(mu.K2[j],tau.K2[j])T(,K1[j])

	mu.K2[j]<-K2_20*exp(lambda2*(Temp[j]-20))
	
	tau.K1[j]~dgamma(1.E-5,1.E-5)
	tau.K2[j]~dgamma(1.E-5,1.E-5)
	}
for (j in 1:NExpsBiph){
	logit(alpha[j])<-(K1[j]-K2[j])*Chgpt[j]
	Chgpt[j]~dunif(Chgpt.L[j],Chgpt.U[j])
	}
for (j in MonoExpStart:NExps){
	alpha[j]<-Flip[j]
	Flip[j]~dbern(theta.alpha[Stud[j]])
	}

#Study level priors
for (k in MonoStudStart:NStud){
	theta.alpha[k]~dbeta(1,1)
	}

#Universal priors	
K1_20~dnorm(0,1.E-6)
K2_20~dnorm(0,1.E-6)#T(,K1_20)
#<-K1_20-delt
#delt<-exp(delta)
#delta~dnorm(0,1.E-3)
lambda1~dnorm(0,1.E-3)
lambda2~dnorm(0,1.E-3)T(,lambda1)
}
